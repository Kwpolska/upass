# Maintainer: Chris Warrick <aur@chriswarrick.com>
pkgbase=python-upass-git
pkgname=('python-upass-git' 'python2-upass-git')
_pyname=upass
_gitname=upass
pkgver=0.1.0
pkgrel=1
pkgdesc='Console UI for pass (git version)'
arch=('any')
url='https://github.com/Kwpolska/upass'
license=('BSD')
makedepends=('git' 'python' 'python2' 'python-setuptools' 'python2-setuptools'
             'python-urwid' 'python2-urwid' 'python-pyperclip'
             'python2-pyperclip')
options=(!emptydirs)
source=("git+https://github.com/Kwpolska/${_gitname}")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/${_gitname}"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-py2"
}

package_python-upass-git() {
  depends=('python' 'python-setuptools' 'python-urwid' 'python-pyperclip')
  provides=('python-upass')
  conflicts=('python-upass')
  cd "${srcdir}/${_gitname}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}

package_python2-upass-git() {
  depends=('python2' 'python2-setuptools' 'python2-urwid' 'python2-pyperclip')
  provides=('python2-upass')
  conflicts=('python2-upass')
  cd "${srcdir}/${_gitname}-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

# vim:set ts=2 sw=2 et:
